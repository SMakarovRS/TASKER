//++ 07.01.2021, Тарасов Михаил: подключение телефонии Дом.РУ к DevBase
&НаКлиенте
Процедура РСК_ПриОткрытииПосле(Отказ)
	Если РС_СозданАвтоматически И ЗначениеЗаполнено(Объект.АбонентКонтакт) Тогда
		ТекущийНомер = Объект.АбонентКакСвязаться;
		КонтактПриИзменении(Неопределено);
		Объект.АбонентКакСвязаться = ТекущийНомер;
	КонецЕсли;
	ОбновитьОтборЗадач();
КонецПроцедуры
//-- 07.01.2021, Тарасов Михаил: подключение телефонии Дом.РУ к DevBase

&НаСервере
Процедура ОбновитьОтборЗадач()
	
	ОтборИнициатор = Неопределено;
	ОтборКлиент = Неопределено;
	Для Каждого ЭлементОтбора Из РС_СписокЗадач.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбора.Представление = "ОтборИнициатор" Тогда
			ОтборИнициатор = ЭлементОтбора;
		ИначеЕсли ЭлементОтбора.Представление = "ОтборКлиент" Тогда
			ОтборКлиент = ЭлементОтбора;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтборИнициатор = Неопределено ИЛИ ОтборКлиент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.АбонентКонтакт) Тогда
		ОтборИнициатор.ПравоеЗначение = Справочники.КонтактныеЛица.ПустаяСсылка();
		ОтборКлиент.ПравоеЗначение = Справочники.Контрагенты.ПустаяСсылка();
		ОтборИнициатор.Использование = Истина;
		ОтборКлиент.Использование = Истина;
	ИначеЕсли РС_ОтборСпискаЗадач = 0 Тогда
		ОтборИнициатор.ПравоеЗначение = Объект.АбонентКонтакт;
	    ОтборИнициатор.Использование = Истина;
		ОтборКлиент.Использование = Ложь;
	Иначе
		ОтборКлиент.ПравоеЗначение = Объект.АбонентКонтакт.Владелец;
	    ОтборКлиент.Использование = Истина;
		ОтборИнициатор.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_АбонентКонтактПриИзмененииПосле(Элемент)
	
	ОбновитьОтборЗадач();
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_РС_ОтборСпискаЗадачПриИзмененииПосле(Элемент)
	
	ОбновитьОтборЗадач();	
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_АбонентКонтактОбработкаВыбораПосле(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбновитьОтборЗадач();
	
КонецПроцедуры

&НаКлиенте
&После("ОбработкаОповещения")
Процедура РСК_ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбранКонтакт" Тогда
		ОбновитьОтборЗадач();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_ПрикрепитьКЗадачеПосле(Команда)
	
	Если Элементы.РС_СписокЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ВзаимодействиеОснование = Элементы.РС_СписокЗадач.ТекущиеДанные.Ссылка;
	Предмет = Объект.ВзаимодействиеОснование;
	Модифицированность = Истина;
	
КонецПроцедуры
