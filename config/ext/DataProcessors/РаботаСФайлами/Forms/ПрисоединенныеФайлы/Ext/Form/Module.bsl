
&НаКлиенте
&ИзменениеИКонтроль("СписокПриАктивизацииСтроки")
Процедура РСК_СписокПриАктивизацииСтроки(Элемент)

	ОбновитьДоступностьФайловыхКоманд();

	#Вставка
	#Если Не ВебКлиент Тогда
		ПредпросмотрДокумента()
	#Иначе
		Элементы.АдресДанныхФайла.Видимость = Предпросмотр;
		Элементы.ПредпросмотрДокументов.Видимость = Ложь;
	#КонецЕсли
	#КонецВставки
КонецПроцедуры

Функция ВернутьДанныеФайла(СсылкаНаФайл)
	
	Расширение = СсылкаНаФайл.Расширение;
	ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(СсылкаНаФайл);
	
	Возврат Новый Структура("Расширение, ДвоичныеДанные", Расширение, ДвоичныеДанные)
	
КонецФункции

&НаКлиенте
Процедура ПредпросмотрДокумента()
	
	#Если Не ВебКлиент Тогда
		ПредпросмотрДокументов = "";
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		ДанныеФайла = ВернутьДанныеФайла(ТекущиеДанные.Ссылка);
		
		Расширение = ДанныеФайла.Расширение;
		ДвоичныеДанные = ДанныеФайла.ДвоичныеДанные; 
		
		ВременныйФайлДоПерезаписи = ПолучитьИмяВременногоФайла(Расширение);
		ДвоичныеДанные.Записать(ВременныйФайлДоПерезаписи);
		
		ЭтоКартинка = Ложь;
		Если Расширение = "docx" Или Расширение = "doc" Тогда
			
			Word = Новый COMОбъект("Word.Application");
			Word.DisplayAlerts = 0;
			Document = Word.Documents.Open(ВременныйФайлДоПерезаписи);
			ВременныйФайл = ПолучитьИмяВременногоФайла(".html");
			Document.SaveAs(ВременныйФайл, 8);
			Word.Quit();
			
		ИначеЕсли Расширение = "xls" Или Расширение = "xlsx" Тогда
			
			Excel = Новый COMОбъект("Excel.Application");
			Excel.DisplayAlerts = 0;
			WorkBook = Excel.WorkBooks.Open(ВременныйФайлДоПерезаписи);
			ВременныйФайл = ПолучитьИмяВременногоФайла(".html");
			WorkBook.SaveAs(ВременныйФайл, 44);
			Excel.Quit();
			
		ИначеЕсли Расширение = "pdf" Тогда
			
			ВременныйФайл = ВременныйФайлДоПерезаписи
			
		Иначе
			
			Картинка = Новый Картинка(ВременныйФайлДоПерезаписи);
			Если Картинка.Формат() = ФорматКартинки.НеизвестныйФормат Тогда
				УдалитьФайлы(ВременныйФайлДоПерезаписи);
				Возврат
			Иначе
				ВременныйФайл = ВременныйФайлДоПерезаписи;
				ЭтоКартинка = Истина
			КонецЕсли
			
		КонецЕсли;
		
		ПредпросмотрДокументов = ВременныйФайл;
		Элементы.АдресДанныхФайла.Видимость = Ложь
	#КонецЕсли
	
КонецПроцедуры
