//++ РС Консалт: Тарасов Михаил 03.06.2021 Задача 4345
//e1cib/data/Документ.Задание?ref=b5a68afa715fa5574ba91f32425b5aea
Процедура ОбработкаКомандыЗвонка(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрКоманды) Тогда
		сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(НСтр("ru = 'Необходимо записать элемент'"));
		Возврат;
	КонецЕсли;	
	
	ДанныеЗаполнения = Новый Структура("Основание", ПараметрКоманды);
	
	ИмяДокументаТелефонныйЗвонок = сфпСофтФонПроСервер.сфпИмяДокументаТелефонныйЗвонок(Истина);
	Если ЗначениеЗаполнено(ИмяДокументаТелефонныйЗвонок) 
		И ТипЗнч(ПараметрКоманды) = Тип(ИмяДокументаТелефонныйЗвонок) Тогда
		
		АбонентКакСвязаться	= сфпСофтФонПроСервер.сфпПолучитьЗначениеРеквизита(ПараметрКоманды, "АбонентКакСвязаться"); 
		Если ЗначениеЗаполнено(АбонентКакСвязаться) Тогда
			ОткрытьФорму("ОбщаяФорма.РС_ФормаВыбораПользователяАТС", Новый Структура("НабираемыйНомер", АбонентКакСвязаться), ЭтотОбъект,,,, Новый ОписаниеОповещения("ПослеВыбораПользователяАТС", ЭтотОбъект, АбонентКакСвязаться), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'В документе не указан номер телефона'"), 5);
		КонецЕсли;
	ИначеЕсли СтрНайти("" + ТипЗнч(ПараметрКоманды), "Задание") > 0 Тогда
		Инициатор = сфпСофтФонПроСервер.сфпПолучитьЗначениеРеквизита(ПараметрКоманды, "Инициатор"); 
		Если ЗначениеЗаполнено(Инициатор) Тогда
			СписокОбъектов = Новый СписокЗначений();
			СписокОбъектов.Добавить(Инициатор);
			ПозвонитьВыбравТелефон(СписокОбъектов);
		КонецЕсли;
	Иначе
		СписокОбъектов = Новый СписокЗначений();
		СписокОбъектов.Добавить(ПараметрКоманды);
		ПозвонитьВыбравТелефон(СписокОбъектов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораПользователяАТС(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиДополнительноеОповещение = Ложь;
	НомерТелефона = ОбработатьНомерТелефона(ДополнительныеПараметры, ВывестиДополнительноеОповещение);
	
	Если НЕ ЗначениеЗаполнено(НомерТелефона) Тогда
		ПоказатьПредупреждение(, "Не удалось корректно обработать номер " + ДополнительныеПараметры,, "Ошибка звонка");
		Возврат;
	КонецЕсли;
	
	Если ВывестиДополнительноеОповещение Тогда
		ПоказатьПредупреждение(, "В телефонном номере указана дополнительная информация: " + ДополнительныеПараметры,, "Дополнительная информация");
	КонецЕсли;
	
	РС_ТелефонияСервер.ПозвонитьПоНомеруЧерезОблачнуюАТС(Результат, НомерТелефона);
	
	
КонецПроцедуры

Функция ОбработатьНомерТелефона(Знач НомерТелефона, ВывестиДополнительноеОповещение = Ложь)
	
	НомерТелефона = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("()_-+ ", СокрЛП(НомерТелефона), "");
	
	Для Сч = 1 По СтрДлина(НомерТелефона) Цикл
		
		Если СтрНайти("0123456789", Сред(НомерТелефона, Сч, 1)) = 0 Тогда
			ВывестиДополнительноеОповещение = Истина;
			НомерТелефона = Лев(НомерТелефона, Сч - 1);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Длина = СтрДлина(НомерТелефона);
	
	Если Длина = 11 И Лев(НомерТелефона, 1) = "8" Тогда
		
		Возврат "7" + Сред(НомерТелефона, 2);
		
	ИначеЕсли Длина = 11 Тогда
		
		Возврат НомерТелефона;
		
	ИначеЕсли Длина = 6 Тогда
		
		Возврат "73812" + НомерТелефона;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Процедура ПозвонитьВыбравТелефон(СписокОбъектов) Экспорт
	
	Если СписокОбъектов.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран абонент'"), 5);
		Возврат;
	КонецЕсли;
	
	СписокТелефонов = РС_ТелефонияСервер.ЗаполнитьСписокТелефонов(СписокОбъектов);	
	Если СписокТелефонов.Количество() = 1 Тогда
		ОткрытьФорму("ОбщаяФорма.РС_ФормаВыбораПользователяАТС", Новый Структура("НабираемыйНомер", СписокТелефонов[0].Значение), ЭтотОбъект,,,, Новый ОписаниеОповещения("ПослеВыбораПользователяАТС", ЭтотОбъект, СписокТелефонов[0].Значение), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	ИначеЕсли СписокТелефонов.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не задан номер телефона у объекта звонка");
	Иначе
		ОписаниеВыбора = Новый ОписаниеОповещения("ПослеВыбораТелефона", ЭтотОбъект); 
		СписокТелефонов.ПоказатьВыборЭлемента(ОписаниеВыбора, НСтр("ru='Выбор телефона'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораТелефона(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.РС_ФормаВыбораПользователяАТС", Новый Структура("НабираемыйНомер", Результат.Значение), ЭтотОбъект,,,, Новый ОписаниеОповещения("ПослеВыбораПользователяАТС", ЭтотОбъект, Результат.Значение), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры
//-- КонецЗадачи 4345