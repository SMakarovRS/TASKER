//++ РС Консалт: Тарасов Михаил 04.02.2021 Задача 443
//e1cib/data/Документ.Задание?ref=9854bf77bab981234918cc24e4396a90

&НаКлиенте
Процедура РС_КомментарийВБуферОбмена(ПараметрКоманды) Экспорт	
	
	Ссылка 			= ПолучитьНавигационнуюСсылку(ПараметрКоманды);
	НомерДокумента 	= УбратьЛидирующиеНули(Реквизит(ПараметрКоманды, "Номер"));
	
	ЭтоЗадача 		= Реквизит(Реквизит(ПараметрКоманды, "Процесс"), "ТипПроцесса") <> ПредопределенноеЗначение("Справочник.ТипыПроцессов.Инцидент");
	ТипДокумента	= ?(ЭтоЗадача, "Задача", "Тикет"); 
	
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь(); 
	
	Комментарий = "//++ РС Консалт: " + ТекущийПользователь + " " + Формат(ТекущаяДата(), "ДЛФ=D") + " " + ТипДокумента + " " + НомерДокумента + "
				|//" + Ссылка + Символы.ПС + "                  
				|//-- Конец" + ?(ЭтоЗадача, "Задачи", "Тикета") + " " + НомерДокумента;
		
	#Если ВебКлиент Тогда
		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПроцедураВозврат", ЭтотОбъект), Комментарий, "Автоматически созданный комментарий",, Истина); 
	#Иначе
		//Если у кого-то не работает вставка в буфер, следует проверить, что в "Панель управления" ->
		// -> "Сеть и Интернет" -> "Свойства браузера" -> "Безопасность" для "Интернет" разрешено 
		//"Разрешить программный доступ к буферу обмена"
		
		Объект = Новый COMОбъект("htmlfile");
		Объект.ParentWindow.ClipboardData.SetData("Text", Комментарий);
		Тест = Объект.ParentWindow.ClipboardData.GetData("Text");
		Если НЕ ЗначениеЗаполнено(Тест) Тогда
			Сообщить("Не удалось поместить комментарий в буфер обмена. Следует проверить, что в ""Панель управления"" -> ""Сеть и Интернет"" -> ""Свойства браузера"" -> ""Безопасность"" для ""Интернет"" разрешено ""Разрешить программный доступ к буферу обмена""");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры
	
	
//Функция исключительно для сокращения размера кода
&НаКлиенте
Функция Реквизит(Ссылка, Имя)
	
	Возврат ОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъектаКлиент(Ссылка, Имя);
	
КонецФункции

&НаКлиенте
Функция УбратьЛидирующиеНули(Знач НомерДокумента)
	
    Пока Лев(НомерДокумента, 1) = "0" Цикл
        НомерДокумента = Прав(НомерДокумента, СтрДлина(НомерДокумента) - 1);
	КонецЦикла;
	
    Возврат НомерДокумента;
КонецФункции

&НаКлиенте
Процедура ПроцедураВозврат(Ответ, Параметр) Экспорт
КонецПроцедуры

//-- КонецЗадачи 443

